{
package parser

import "github.com/arizonahanson/oryx/pkg/ast"
}

AST ←  seq:Seq EOF {
  return ast.Expr(seq.([]ast.Any)), nil
}

// all types
Any ←  Null / Boolean / Number / String / Array / Map / Symbol / Expr

// sequence without delimiters
Seq ←  _* first:Any rest:(_+ Any)+ _* {
  return join(first, rest, 1), nil
} / _* infix:Infix _* {
  return infix, nil
}

// s-expression
Expr ←  '(' seq:Seq ')' {
  return ast.Expr(seq.([]ast.Any)), nil
} / '(' Seq !')' {
  return ast.Null{}, errors.New("not terminated")
}
// infix order of operations
Infix ←  OrExpr
// or
OrOp ←  "||" {
  return symbol(c)
}
OrExpr ←  first:AndExpr rest:(_* OrOp _* AndExpr)* {
  return swap(first, rest, 1, 3), nil
}
// and
AndOp ←  "&&" {
  return symbol(c)
}
AndExpr ←  first:EqlExpr rest:(_* AndOp _* EqlExpr)* {
  return swap(first, rest, 1, 3), nil
}
// equal / not-equal
EqlOp ←  ("==" / "!=") {
  return symbol(c)
}
EqlExpr ←  first:CmpExpr rest:(_* EqlOp _* CmpExpr)? {
  return swap(first, []interface{}{rest}, 1, 3), nil
}
// comparisons
CmpOp ←  ("<=" / "<" / ">=" / ">") {
  return symbol(c)
}
CmpExpr ←  first:AddExpr rest:(_* CmpOp _* AddExpr)? {
  return swap(first, []interface{}{rest}, 1, 3), nil
}
// add / subtract
AddOp ←  ("+" / "-") {
  return symbol(c)
}
AddExpr ←  first:MulExpr rest:(_* AddOp _* MulExpr)* {
  return swap(first, rest, 1, 3), nil
}
// multiply / divide
MulOp ←  ("*" / "/") {
  return symbol(c)
}
MulExpr ←  first:UnExpr rest:(_* MulOp _* UnExpr)* {
  return swap(first, rest, 1, 3), nil
}
// unary
UnOp ←  "!" {
  return symbol(c)
}
UnExpr ←  op:UnOp? _* any:Any? {
  res := []ast.Any{}
  if op != nil {
    res = append(res, op.(ast.Symbol))
  }
  if any != nil {
    res = append(res, any.(ast.Any))
  }
	return res, nil
}

// array
Array ←  '[' seq:Seq ']' {
  return ast.Array(seq.([]ast.Any)), nil
} / '[' Seq !']' {
  return ast.Null{}, errors.New("not terminated")
}

// map
Map ←  '{' _* first:(String _* ':' _* Any)? rest:(_+ String _* ':' _* Any)* _* '}' {
  return ast.Map(merge(first, rest, 0, 4)), nil
} / '{' _* (String _* ':' _* Any) (_+ String _* ':' _* Any)* _* !'}' {
  return ast.Null{}, errors.New("not terminated")
}

// null
Null ←  "null" {
  return ast.Null{}, nil
}

// boolean
Boolean ←  "true" {
  return ast.Boolean(true), nil
} / "false" {
  return ast.Boolean(false), nil
}

// real number (eg. -123.45e-67)
Number ←  '-'? digit+ ('.' digit+)? ('e'i ('+' / '-')? digit+)? {
	return ast.NewNumberFromString(string(c.text))
}

// quoted string
String ←  '"' runeChr* '"' {
  return ast.NewStringFromString(string(c.text))
} / '"' runeChr* !'"' {
	return ast.Null{}, errors.New("not terminated")
}
// no naked " or \ inside a String, supports \\, \/, \", \abfnrtv, \xff, \uffff, \Uffffffff
runeChr ←  [^"\\] / runeEsc
runeEsc ←  `\` (["\\/abfnrtv] /
           ('x' hexDigit hexDigit) /
           ('u' hexDigit hexDigit hexDigit hexDigit) /
           ('U' hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit))
hexDigit ← [0-9a-f]i

// symbol
Symbol ←  !(Null / Boolean) word ("!" / "?")? {
  return symbol(c)
}

// symbol component
word ←  letter (letter / digit)*
// unicode "letters" for symbols
letter ←  [\p{L}] / '_'
// numerals
digit ←  [0-9]

// UTF-8: whitespace and control chars/unused codes
_ "whitespace" ←  [\p{Z}] / [\p{C}] / ',' / Comment
// comments
Comment ←  SingleLineComment / MultiLineComment
SingleLineComment ← "//" (!EOL .)* EOL
MultiLineComment ←  "/*" (!"*/" .)* "*/"

// end of line
EOL ←  '\n' / EOF
// End of file
EOF ←  !.
