{
package parser

import "github.com/arizonahanson/oryx/pkg/ast"
}

AST ←  seq:(Seq / Infix) EOF {
  return ast.Expr(seq.([]ast.Any)), nil
}

// all types
Any ←  Null / Boolean / Number / String / Array / Map / Symbol / Expr

// sequence without delimiters
Seq ←  _* first:Any? rest:(_+ Any)* _* {
  return join(first, rest, 1), nil
}

// s-expression
Expr ←  '(' seq:(Seq / Infix) ')' {
  return ast.Expr(seq.([]ast.Any)), nil
} / '(' (Seq / Infix) !')' {
  return ast.Null{}, errors.New("not terminated")
}
// infix order of operations
Infix ←  OrExpr
// or
OrOp ←  "||" {
  return ast.Symbol{Val:string(c.text), Pos:pos(c.pos)}, nil
}
OrExpr ←  _* first:AndExpr rest:(_+ OrOp _+ AndExpr)* _* {
  return swap(first, rest, 1, 3), nil
}
// and
AndOp ←  "&&" {
  return ast.Symbol{Val:string(c.text), Pos:pos(c.pos)}, nil
}
AndExpr ←  _* first:EqlExpr rest:(_+ AndOp _+ EqlExpr)* _* {
  return swap(first, rest, 1, 3), nil
}
// equal / not-equal
EqlOp ←  ("==" / "!=") {
  return ast.Symbol{Val:string(c.text), Pos:pos(c.pos)}, nil
}
EqlExpr ←  _* first:CmpExpr rest:(_+ EqlOp _+ CmpExpr)? _* {
  return swap(first, rest, 1, 3), nil
}
// comparisons
CmpOp ←  ("<" / "<=" / ">" / ">=") {
  return ast.Symbol{Val:string(c.text), Pos:pos(c.pos)}, nil
}
CmpExpr ←  _* first:AddExpr rest:(_+ CmpOp _+ AddExpr)? _* {
  return swap(first, rest, 1, 3), nil
}
// add / subtract
AddOp ←  ("+" / "-") {
  return ast.Symbol{Val:string(c.text), Pos:pos(c.pos)}, nil
}
AddExpr ←  _* first:MulExpr rest:(_+ AddOp _+ MulExpr)* _* {
  return swap(first, rest, 1, 3), nil
}
// multiply / divide / modulo
MulOp ←  ("*" / "/" / "%") {
  return ast.Symbol{Val:string(c.text), Pos:pos(c.pos)}, nil
}
MulExpr ←  _* first:Any rest:(_+ MulOp _+ Any)* _* {
  return swap(first, rest, 1, 3), nil
}

// array
Array ←  '[' seq:Seq ']' {
  return ast.Array(seq.([]ast.Any)), nil
} / '[' Seq !']' {
  return ast.Null{}, errors.New("not terminated")
}

// map
Map ←  '{' _* first:(String _* ':' _* Any)? rest:(_+ String _* ':' _* Any)* _* '}' {
  return ast.Map(merge(first, rest, 0, 4)), nil
} / '{' _* (String _* ':' _* Any) (_+ String _* ':' _* Any)* _* !'}' {
  return ast.Null{}, errors.New("not terminated")
}

// null
Null ←  "null" {
  return ast.Null{}, nil
}

// boolean
Boolean ←  "true" {
  return ast.Boolean(true), nil
} / "false" {
  return ast.Boolean(false), nil
}

// real number (eg. -123.45e-67)
Number ←  '-'? digit+ ('.' digit+)? ('e'i ('+' / '-')? digit+)? {
	return ast.NewNumberFromString(string(c.text))
}

// quoted string
String ←  '"' runeChr* '"' {
  return ast.NewStringFromString(string(c.text))
} / '"' runeChr* !'"' {
	return ast.Null{}, errors.New("not terminated")
}
// no naked " or \ inside a String, supports \\, \/, \", \abfnrtv, \xff, \uffff, \Uffffffff
runeChr ←  [^"\\] / runeEsc
runeEsc ←  `\` (["\\/abfnrtv] /
           ('x' hexDigit hexDigit) /
           ('u' hexDigit hexDigit hexDigit hexDigit) /
           ('U' hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit))
hexDigit ← [0-9a-f]i

// symbol
Symbol ←  !(Null / Bool) word suffix? {
  return ast.Symbol{Val: string(c.text), Pos: pos(c.pos)}, nil
}

// symbol component
word ←  letter (letter / digit)*
// unicode "letters" for symbols
letter ←  [\p{L}] / '_'
// numerals
digit ←  [0-9]

// UTF-8: whitespace and control chars/unused codes
_ "whitespace" ←  [\p{Z}] / [\p{C}] / ',' / Comment
// comments
Comment ←  SingleLineComment / MultiLineComment
SingleLineComment ← "//" (!EOL .)* EOL
MultiLineComment ←  "/*" (!"*/" .)* "*/"

// end of line
EOL ←  '\n' / EOF
// End of file
EOF ←  !.
